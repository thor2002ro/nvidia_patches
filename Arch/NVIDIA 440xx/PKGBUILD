# Maintainer: Isaak I. Aleksandrov

pkgname=nvidia-dkms
pkgver=440.82
pkgrel=420
pkgdesc="NVIDIA drivers - module sources"
arch=('x86_64')
url="https://www.nvidia.com/"
depends=('dkms' "${pkgname//-dkms*}-utils=$pkgver" 'libglvnd')
optdepends=('linux-headers: Build the module for Arch kernel'
            'linux-hardened-headers: Build the module for Hardened Arch kernel'
            'linux-lts-headers: Build the module for LTS Arch kernel'
            'linux-zen-headers: Build the module for ZEN Arch kernel')
conflicts=("${pkgname//-dkms*}"
           "${pkgname//-dkms*}-hardened"
           "${pkgname//-dkms*}-lts"
           "${pkgname//-dkms*}-zen")
provides=('NVIDIA-MODULE' "${pkgname//-dkms*}=$pkgver")
license=('custom')
options=('!strip')
_pkg="NVIDIA-Linux-x86_64-${pkgver}"
source=("https://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/${_pkg}.run"
        "https://gitlab.com/EULA/snippets/-/raw/master/NVIDIA/$pkgver/kernel-5.7.patch"
        "https://gitlab.com/EULA/snippets/-/raw/master/NVIDIA/$pkgver/kernel-5.8.patch")
sha512sums=('d86ed2cd715c5a9aebdd11ee562cfa454dbafdb1b468004cbb93d37ee258623f11144cf30b8b14996a4e093cb3119edc36d13152893d735e3536f49c45e2cca3'
            'a010fc217ace313350aca05e3225c328ca98d5ce636d3742fc7a9e353e9af8d9cdd5424bee6dadbe57c8c0cb58f1f204129bf8ac25848256053cae31c286b8cd'
            '99a6fc7b3ad3980bfb72daed1c32966ab967dad7dfcdf6b2d6aba1607bc56f7d487693a02f6c2bd9b774fdebfa8579ca337e6dfc76842dcdd010fefa6a79f9d3')

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"

    # https://gitlab.com/EULA/snippets
    patch -p1 -i ../kernel-5.7.patch
    patch -p1 -i ../kernel-5.8.patch

    cd kernel
    sed -i "s/__VERSION_STRING/${pkgver}/" dkms.conf
    sed -i 's/__JOBS/`nproc`/' dkms.conf
    sed -i 's/__DKMS_MODULES//' dkms.conf
    sed -i '$iBUILT_MODULE_NAME[0]="nvidia"\
DEST_MODULE_LOCATION[0]="/kernel/drivers/video"\
BUILT_MODULE_NAME[1]="nvidia-uvm"\
DEST_MODULE_LOCATION[1]="/kernel/drivers/video"\
BUILT_MODULE_NAME[2]="nvidia-modeset"\
DEST_MODULE_LOCATION[2]="/kernel/drivers/video"\
BUILT_MODULE_NAME[3]="nvidia-drm"\
DEST_MODULE_LOCATION[3]="/kernel/drivers/video"' dkms.conf

    # Gift for linux-rt guys
    sed -i 's/NV_EXCLUDE_BUILD_MODULES/IGNORE_PREEMPT_RT_PRESENCE=1 NV_EXCLUDE_BUILD_MODULES/' dkms.conf
}

package() {
    cd ${_pkg}

    install -dm 755 "${pkgdir}"/usr/src
    cp -dr --no-preserve='ownership' kernel "${pkgdir}/usr/src/nvidia-${pkgver}"

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 "${srcdir}/${_pkg}/LICENSE"
}
